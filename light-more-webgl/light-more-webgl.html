<!doctype html>
<html>
    <head>
        <meta charset="UTF-8"/>
        <title>More Normals and Specular Lighting in WebGL</title>
        <link rel="stylesheet" href="light-more-webgl.css" />
    </head>
    <body>
        <h1>More Normals and Specular Lighting in WebGL</h1>

        <p>Youâ€™ll need a WebGL-capable web browser to see anything here;
            instructions can be found in this
            <a href="http://www.khronos.org/webgl/wiki/Getting_a_WebGL_Implementation">official
            WebGL wiki page</a>.</p>

        <p>This page demonstrates an approach toward automatically generating normals plus the
            implementation of specular lighting in WebGL.</p>

        <!-- Note the rectangular canvas now. -->
        <canvas id="light-more-webgl" width="1024" height="512">
            Sorry, your web browser does not appear to support
            the <code>canvas</code> element, either.
        </canvas>

        <!-- jQuery makes some things more convenient. -->
        <script src="http://code.jquery.com/jquery-latest.min.js"></script>

        <!-- GLSLUtilities is a starting point for holding functionality that
             is common to many WebGL programs. -->
        <script src="glsl-utilities.js"></script>

        <!-- We use our vector library here. -->
        <script src="../vector/vector.js"></script>

        <!-- Shapes is a library that generates "canned" shapes. -->
        <script src="shapes.js"></script>

        <!-- Set up shaders: we've placed the source in script elements for
            simplicity.  Shaders are ideally kept in separate files for easier
            maintenance, but in that situation, additional code and security
            constraints are involved. -->

        <!-- The vertex shader now includes some lighting calculations. -->
        <script id="vertex-shader" type="x-shader/x-vertex">
            #ifdef GL_ES
            precision highp float;
            #endif

            // Note the normal vector.
            attribute vec3 vertexPosition;
            attribute vec3 vertexDiffuseColor;
            attribute vec3 vertexSpecularColor;
            attribute vec3 normalVector;

            // The should-now-be-familiar matrices.
            uniform mat4 projectionMatrix;
            uniform mat4 modelViewMatrix;
            uniform mat4 xRotationMatrix;
            uniform mat4 yRotationMatrix;

            // We don't calculate color in the vertex shader anymore.  Instead,
            // we will pass on information that will affect the color later.
            varying vec3 fragmentDiffuseColor;
            varying vec3 fragmentSpecularColor;
            varying vec3 fragmentNormal;
            varying vec3 fragmentEye;
            varying vec3 fragmentLight;

            // We only need light position in the vertex shader now.  This time,
            // we copy the fixed-function convention for using the fourth component
            // to indicate position vs. direction, so we take a vec4 here instead.
            uniform vec4 lightPosition;

            void main(void) {
                // We "merely" relay these vectors to the fragment shader.  However,
                // we get a cool trick: they get interpolated for us!
                fragmentNormal = normalVector;
                fragmentEye = vertexPosition;
                fragmentLight = lightPosition.xyz;

                // No conditional expressions in GLSL, unfortunately.  We have to
                // do this the other way.
                if (lightPosition.w != 0.0) {
                    // Calculate the direction vector.
                    fragmentLight = lightPosition.xyz - vertexPosition;
                }

                // We now defer all color calculations to the fragment shader.
                fragmentDiffuseColor = vertexDiffuseColor;
                fragmentSpecularColor = vertexSpecularColor;
                gl_Position = projectionMatrix *
                        xRotationMatrix * yRotationMatrix *
                        modelViewMatrix * vec4(2.0 * vertexPosition, 1.0);
            }
        </script>

        <!-- For now, the fragment shader just takes the incoming color.
             Remember that interpolation is automatic. -->
        <script id="fragment-shader" type="x-shader/x-fragment">
            #ifdef GL_ES
            precision highp float;
            #endif

            varying vec3 fragmentDiffuseColor;
            varying vec3 fragmentSpecularColor;
            varying vec3 fragmentNormal;
            varying vec3 fragmentEye;
            varying vec3 fragmentLight;

            // Color settings come in here now.
            uniform vec3 lightDiffuse;
            uniform vec3 lightSpecular;

            // Now with shininess!
            uniform float shininess;

            void main(void) {
                // Normalize our vectors.
                vec3 n = normalize(fragmentNormal);
                vec3 e = normalize(fragmentEye);
                vec3 l = normalize(fragmentLight);
                // Use h = normalize(l + e) for the Blinn-Phong approximation.
                vec3 r = 2.0 * dot(l, n) * n - l;

                // Figure out the color of the diffuse light that lands on the vertex.
                vec3 diffuseProduct = fragmentDiffuseColor * lightDiffuse;

                // Calculate the amount of diffuse light that hits the vertex.
                float kd = max(dot(l, n), 0.0);
                vec3 finalDiffuse = kd * diffuseProduct;

                // Figure out the color of the specular light that lands on the vertex.
                vec3 specularProduct = fragmentSpecularColor * lightSpecular;

                // Calculate the amount of specular light that hits the vertex.
                // Use dot(n, h) for the Blinn-Phong approximation.
                float ks = pow(max(dot(r, e), 0.0), shininess);
                vec3 finalSpecular = ks * specularProduct;
                if (dot(l, n) < 0.0) {
                    finalSpecular = vec3(0.0, 0.0, 0.0);
                }

                // .rgb suffix not really necessary, but included for emphasis.
                gl_FragColor = vec4(finalDiffuse.rgb + finalSpecular.rgb, 1.0);
            }
        </script>

        <!-- This script sets up the specific scene for this page. -->
        <script src="light-more-webgl.js"></script>
  </body>
</html>
